def safeExtGet(prop, fallback) {
  rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

buildscript {
  // The Android Gradle plugin is only required when opening the android folder stand-alone.
  // This avoids unnecessary downloads and potential conflicts when the library is included as a
  // module dependency in an application project.
  if (project == rootProject) {
    repositories {
      google()
      mavenCentral()
    }
    dependencies {
      classpath("com.android.tools.build:gradle:7.1.1")
    }
  }
}

apply plugin: 'com.android.library'

android {
  compileSdkVersion safeExtGet('compileSdkVersion', 31)

  defaultConfig {
    minSdkVersion safeExtGet('minSdkVersion', 21)
    targetSdkVersion safeExtGet('targetSdkVersion', 31)
  }
}

repositories {
  maven {
    // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
    url("$rootDir/../node_modules/react-native/android")
  }
  maven {
    // Android JSC is installed from npm
    url("$rootDir/../node_modules/jsc-android/dist")
  }
  mavenCentral {
    // We don't want to fetch react-native from Maven Central as there are
    // older versions over there.
    content {
      excludeGroup "com.facebook.react"
    }
  }
  google()
  maven { url 'https://www.jitpack.io' }
}

dependencies {
  implementation 'com.facebook.react:react-native:+'
  implementation('com.google.android.gms:play-services-base') {
    version {
      strictly '[18.0.1, 19.0.0['
      prefer '18.2.0'
    }
  }
  implementation('com.google.android.gms:play-services-maps') {
    version {
      strictly '[18.0.0, 19.0.0['
      prefer '18.0.2'
    }
  }
  implementation('com.google.android.gms:play-services-location') {
    version {
      strictly '[20.0.0, 21.0.0['
      prefer '20.0.0'
    }
  }
  implementation('com.google.maps.android:android-maps-utils') {
    version {
      strictly '[3.0.0, 4.0.0['
      prefer '3.4.0'
    }
  }
  implementation('androidx.work:work-runtime') {
    version {
      strictly '[2.7.1, 3.0.0['
      prefer '2.7.1'
    }
  }
}
